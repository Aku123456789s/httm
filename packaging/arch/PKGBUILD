pkgname=httm
pkgver=0.11.1
pkgrel=1
pkgdesc="*The dream of a CLI ZFS Time Machine is still alive with httm.* httm prints the size, date and corresponding locations of available unique versions (deduplicated by modify time and size) of files residing on ZFS snapshots, but can also be used interactively to select and restore such files."
arch=('x86_64')
url="https://github.com/kimono-koans/httm/blob/master/README.md"
license=('MPL-2.0')
groups=('utility')
options=('!strip' '!emptydirs')
makedepends=('cargo')
source=("${pkgver}.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate")
sha512sums=('3f3cc3d185eeec6d4c034c7a6e5a715b9f04af528c190d0f242b1cdfa2e560624c4f3bd7d53e94d9bde4065f400320c306fba95788cc4e2e33c645399442334b')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
	cd -
}

build(){
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	# use cargo to build from a tagged release
	cd "${srcdir}/${pkgname}-${pkgver}"
	cargo build --frozen --release --all-features -- -C strip=symbols
	cd -
}

package(){
	# install executable
	install -Dm755 "${srcdir}/${pkgname}-${pkgver}/target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

	# install man page
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

	# install README.md
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"

	# install LICENSE
	install -Dm644 "${srcdir}/${pkgname}-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}